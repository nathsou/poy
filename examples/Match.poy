import Foundations { print, Option }

let a = match 3 {
    1 => "a",
    2 => "b",
    3 => "c",
    _ => "?",
}

let b = match (9, 7) {
    (2, 3) => 1,
    (2, _) => 2,
    (a, b) => a * b,
}

let c = match "yolo" {
    "yo" => 1,
    "lo" => 2,
    "yolo" => 3,
    _ => 4,
}

let d = match 3 * 7 == 21 {
    true => (2, 3, 5),
    false => (7, 11, 13),
}

let e = if 1 == 0 { 1 } else if 1 == 1 { 2 } else { 3 }

let f = .some(3).map(n -> n * 7)
let g = .none

struct Yolo { a: Num, b: (Num, Num) }

let h = match (Yolo { a: 2, b: (3, 5) }, (7, (11, (13, 17)))) {
    ({ a, b: (b, c) }, (d, (e, (f, g)))) => a * b * c * d * e * f * g,
}

let i = use (pi, d) = (3.1415926, 22) in d / pi
let (j, (k, l)) = (3, ("yo", true))

struct Vec2 { x: Num, y: Num }

extend Vec2 {
    fun add({ x, y }: Vec2) {
        Vec2 { x: self.x + x, y: self.y + y }
    }
}

let m = Vec2 { x: 3, y: 5 }.add(Vec2 { x: 7, y: 11 })

enum Expr {
    num(Num),
    add(Expr, Expr),
    sub(Expr, Expr),
    mul(Expr, Expr),
    div(Expr, Expr),
    equ(Expr, Expr),
    neq(Expr, Expr),
    lss(Expr, Expr),
    gtr(Expr, Expr),
    ifThenElse(Expr, Expr, Expr),
}

extend Bool {
    fun toNum() {
        if self { 1 } else { 0 }
    }
}

extend Expr {
    fun eval() {
        match self {
            .num(n) => n,
            .add(a, b) => a.eval() + b.eval(),
            .sub(a, b) => a.eval() - b.eval(),
            .mul(a, b) => a.eval() * b.eval(),
            .div(a, b) => a.eval() / b.eval(),
            .equ(a, b) => (a.eval() == b.eval()).toNum(),
            .neq(a, b) => (a.eval() != b.eval()).toNum(),
            .lss(a, b) => (a.eval() < b.eval()).toNum(),
            .gtr(a, b) => (a.eval() > b.eval()).toNum(),
            .ifThenElse(cond, a, b) => (if cond.eval() == 1 { a } else { b }).eval(),
        }
    }
}

let n = .gtr(.add(.num(3), .mul(.num(5), .num(7))), .num(37)).eval()

print((a, b, c, d, e, f, g, f.isSome(), g.isNone(), h, i, j, k, l, m, n))
